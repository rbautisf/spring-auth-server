plugins {
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'idea'
    id 'java-library' //includes 'java'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'maven-publish'
}

group = 'com.nowhere'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

// refer to https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java{
            srcDir('src/intTest/java')
        }
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

idea {
    module {
        testSources.from(sourceSets.intTest.java.srcDirs)
    }
}
// intTest config ends here

dependencies {
    implementation ('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'){
        //remove warn  issue Provides transitive vulnerable dependency maven:org.springframework:spring-core:6.1.2
        exclude group: 'org.springframework', module: 'spring-core'
    }
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation 'org.springframework.session:spring-session-data-redis'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'

    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation 'org.springframework.security:spring-security-test'
    intTestImplementation 'com.h2database:h2'
    intTestImplementation 'net.sourceforge.htmlunit:htmlunit'
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation "org.springframework.boot:spring-boot-starter-data-redis"
    intTestImplementation 'org.springframework.session:spring-session-data-redis'
    intTestImplementation project.getDependencies().testFixtures(project)
    intTestImplementation 'it.ozimov:embedded-redis:0.7.3' exclude(group: 'org.slf4j', module: 'slf4j-simple')

    // add jakarta.persistence.EnumType to testFixtures
    testFixturesImplementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    testFixturesImplementation 'org.springframework.security:spring-security-test'
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'

}
test {
    useJUnitPlatform()
}
tasks.register('intTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

jacocoTestReport {
    executionData(test, intTest)
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
    // Issue with CSS3ParserTokenManager with JaCoCo 0.8.11.202310140853/f33756c.
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['com/gargoylesoftware/css/parser/javacc/CSS3ParserTokenManager.class'])
        }))
    }
    dependsOn (test, intTest)
}

// https://docs.github.com/en/packages/working-with-a-github-packages-registry/working-with-the-gradle-registry#example-using-gradle-groovy-for-a-single-package-in-a-repository
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/rbautisf/spring-auth-server")
            credentials {
                username = System.getenv("GIT_USERNAME")
                password = System.getenv("GIT_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
